
    model{
        ## system of ODEs resolution
        solution[1:ntimes,1:Nesp] <- ode(VALinits[1:Nesp], 
                                   tgrid[1:ntimes], 
                                   D(C[1:Nesp], s1), ## s1 est le pas de temps de lequa diff (dt)
                                   origin, 
                                   tol) 
        ## Lokta-voltera model
        D(C[1], s1) <- alpha*C[1] - beta*C[1]*C[2]
        D(C[2], s1) <- -gamma*C[2] + delta*C[1]*C[2]

        ## observation
        ### initial conditions
        Lynxinit ~ dlnorm(VALinits[2],tauLynx)
        Lievreinit ~ dlnorm(VALinits[1],tauLievre)
        ### loop over time
        for (t in 1:Nyear){
            Lynx[t] ~ dlnorm(solution[t,2],tauLynx)
            Lievre[t] ~ dlnorm(solution[t,1],tauLievre)
            #repeted (simulated) data
            Lynxrep[t] ~ dlnorm(solution[t,2],tauLynx) 
            Lievrerep[t] ~ dlnorm(solution[t,1],tauLievre)
            #chi2 distance
            evalLynx[t] <- solution[t,2]
            ELynx[t] <- pow(Lynx[t]-evalLynx[t],2)/(evalLynx[t]+0.5)
            ELynxrep[t] <- pow(Lynxrep[t]-evalLynx[t],2)/(evalLynx[t]+0.5)
            evalLievre[t] <- solution[t,2]
            ELievre[t] <- pow(Lievre[t]-evalLievre[t],2)/(evalLievre[t]+0.5)
            ELievrerep[t] <- pow(Lievrerep[t]-evalLievre[t],2)/(evalLievre[t]+0.5)
        }
        #fit variables
        fitLynx <- sum(ELynx[])
        fitLynxrep <- sum(ELynxrep[])
        fitLievre <- sum(ELievre[])
        fitLievrerep <- sum(ELievrerep[])

        ## a priori
        tauLynx <- 1/(sigLynx*sigLynx)
        sigLynx ~ dunif(0,4)
        tauLievre <- 1/(sigLievre*sigLievre)
        sigLievre ~ dunif(0,4)
        VALinits[1] ~ dunif(0,100)
        VALinits[2] ~ dunif(0,100)
        alpha ~ dunif(0,1)
        beta ~ dunif(0,1)
        gamma ~ dunif(0,1)
        delta ~ dunif(0,1)
    }
